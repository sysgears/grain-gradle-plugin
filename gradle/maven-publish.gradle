apply plugin: 'maven'
apply plugin: 'signing'

jar {
    manifest {
        attributes 'Implementation-Title': 'Grain Gradle plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    archives groovydocJar
}

signing {
    sign configurations.archives
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

uploadArchives {
    repositories.mavenDeployer {

        configuration = configurations.deployerJars

        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: System.getProperty('sonatypeUsername'), password: System.getProperty('sonatypePassword'))
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: System.getProperty('sonatypeUsername'), password: System.getProperty('sonatypePassword'))
        }

        pom.project {
            name 'Grain Gradle plugin'
            description '''Gradle plugin for Grain general purpose static site generator which provides
all the modern features and tools for generating any kind of HTML content.'''
            url 'http://www.sysgears.com/grain'
            inceptionYear '2014'

            scm {
                url 'https://github.com/sysgears/grain-gradle-plugin'
                connection 'scm:https://github.com/sysgears/grain-gradle-plugin.git'
                developerConnection 'scm:git@github.com:sysgears/grain-gradle-plugin.git'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'sysgears'
                    name 'SysGears LLC'
                    email 'info@sysgears.com'
                }
            }
        }
    }
}